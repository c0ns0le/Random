#!/opt/sam/perl/5.16.0/gcc41/bin/perl
##!/usr/bin/env perl
use strict;
use warnings;
# Description: Gather info (CPU cores, RAM amount, etc.) of compute nodes
# Written by: Jeff White of the University of Pittsburgh (jaw171@pitt.edu)
# Version: 1.0
# Last change: Initial version

# License
# This script is released under version three of the GNU General Public License (GPL) of the 
# Free Software Foundation (FSF), the text of which is available at http://www.fsf.org/licensing/licenses/gpl-3.0.html.
# Use or modification of this script implies your acceptance of this license and its terms.
# This is a free script, you are free to change and redistribute it with the terms of the GNU GPL.
# There is NO WARRANTY, not even for FITNESS FOR A PARTICULAR USE to the extent permitted by law.

# The output for each node is: Node,MAC #1,MAC #2,CPU Type,CPU Cores,GPU?,IB?,Scratch Disk (GB),RAM (GB),Hardware Manufacturer,Model Number,Serial Number,Ethernet IP,Rack Location

use Getopt::Long;
Getopt::Long::Configure("bundling");

# Don't change these
$| = 0;

GetOptions('h|help' => \my $helpopt,
           's|sum' => \my $sum,
          ) || die "Invalid usage, use -h for help.\n";

if ($helpopt) {
  print "Gather info (CPU cores, RAM amount, etc.) of compute nodes.\n";
  print "Run this script via beorun to get the node details via STDOUT.\n";
  print "License: GNU General Public License (GPL) v3.\n\n";
  print "Usage: $0 [options]\n"; 
  print "-h | --help : Show this help\n";
  print "-s | --sum : Display sums (total CPU count, etc.) of a CSV generated by this script\n";
  exit;
}

#
# Print the sum if we were asked to
#
if ($sum) {
  
  my %totals;
  $totals{"CPU core count"} = 0;
  $totals{"Scratch disk (GB)"} = 0;
  $totals{"RAM (GB)"} = 0;
  
  # Get the totals from the input
  for my $line (<>) {
    my ($cpu_cores, $scratch, $ram) = (split(m/,/, $line))[4,7,8];
    
    $cpu_cores =~ s/ CPU cores$//;
    $scratch =~ s/G scratch$//;
    $ram =~ s/G RAM$//;
    
    $totals{"CPU core count"} = $totals{"CPU core count"} + $cpu_cores;
    $totals{"Scratch disk (GB)"} = $totals{"Scratch disk (GB)"} + $scratch;
    $totals{"RAM (GB)"} = $totals{"RAM (GB)"} + $ram;
  }

  # Print the totals we gathered
  for my $key (keys(%totals)){
    print "$key: $totals{$key}\n";
  }
  
  exit;
}


#
# Node number
#
my $hostname = `hostname`;
chomp $hostname;

# Are we on a compute node?
die "Not running on a compute node..." unless ($hostname =~ m/^n\d/);

print "$hostname,";


#
# MAC of eth0
#
open(my $ETH0_MAC_FILE, "<", "/sys/class/net/eth0/address");
my $eth0_mac = <$ETH0_MAC_FILE>;
close $ETH0_MAC_FILE;

chomp $eth0_mac;
print "$eth0_mac,";


#
# MAC of eth1
#
open(my $ETH1_MAC_FILE, "<", "/sys/class/net/eth1/address");
my $eth1_mac = <$ETH1_MAC_FILE>;
close $ETH1_MAC_FILE;

chomp $eth1_mac;
print "$eth1_mac,";


#
# Type of CPU
#
open(my $CPU_INFO_FILE, "<", "/proc/cpuinfo");
my @cpu_info = <$CPU_INFO_FILE>;
close $CPU_INFO_FILE;

for my $line (@cpu_info) {
  chomp $line;
  
  # Skip everything except the model name
  next unless ($line =~ m/^model name/);
  
  my $cpu_model = $line;
  $cpu_model =~ s/^model name\s+:\s+//;
  
  print "$cpu_model,";
  
  last;
}


#
# Number of CPU cores
#
my $num_physical_cpus = grep(m/^Processor Information/, `dmidecode`);

# If it looks like a multi-core CPU...
if (grep(m/^siblings/, @cpu_info)) {
  for my $line (@cpu_info) {
    chomp $line;

    # Skip everything except the number of siblings
    next unless ($line =~ m/^siblings/);
    
    my $cpu_siblings = $line;
    $cpu_siblings =~ s/^siblings\s+:\s+//;
    
    print $num_physical_cpus * $cpu_siblings, " CPU cores,";
    
    last;
  }
}
# ...otherwise just print the number of physical CPUs
else {
  print "$num_physical_cpus CPU cores,";
}


#
# GPU found?
#
if (grep(m/^nvidia/, `lsmod`)) {
  print "Yes GPU,";
}
else {
  print "No GPU,";
}


#
# Infiniband found?
#
if (-d "/sys/class/net/ib0") {
  print "Yes Infiniband,";
}
else {
  print "No Infiniband,";
}


#
# Amount of scratch disk
#
my @df_out = `df -kP /scratch`;
if ($df_out[1]) {
  my $scratch_size = (split(m/\s+/, $df_out[1]))[1];
  $scratch_size = sprintf("%.2f", $scratch_size / 1024 / 1024);
  print "${scratch_size}G scratch,";
}
else {
  print "0G scratch,";
}


#
# Amount of RAM
#
open(my $MEMINFO_FILE, "<", "/proc/meminfo");
for my $line (<$MEMINFO_FILE>) {
  chomp $line;

  # Skip everything except the memtotal
  next unless ($line =~ m/^MemTotal:\s+(\d+)/);
  
  # Convert to GB and slice off all but two decimal points
  my $memtotal = sprintf("%.2f", $1 / 1024 / 1024);
  
  # Ugly but it works...
  $memtotal = 128 if ($memtotal == 125.36);
  $memtotal = 128 if ($memtotal == 125.98);
  $memtotal = 112 if ($memtotal == 109.58);
  $memtotal = 120 if ($memtotal == 118.09);
  $memtotal = 6 if ($memtotal == 5.87);
  $memtotal = 6 if ($memtotal == 5.82);
  $memtotal = 5 if ($memtotal == 4.84);
  $memtotal = 48 if ($memtotal == 47.16);
  $memtotal = 48 if ($memtotal == 47.14);
  $memtotal = 256 if ($memtotal == 252.04);
  $memtotal = 64 if ($memtotal == 62.95);
  $memtotal = 12 if ($memtotal == 11.72);
  $memtotal = 24 if ($memtotal == 23.53);
  $memtotal = 20 if ($memtotal == 19.59);
  $memtotal = 8 if ($memtotal == 7.80);
  $memtotal = 16 if ($memtotal == 15.67);
  $memtotal = 14 if ($memtotal == 13.70);
  
  print "${memtotal}G RAM,";
}


#
# Hardware manufacturer
#
my $node_number = $hostname;
$node_number =~ s/^n//;
if ((($node_number >= 0) and ($node_number <= 176)) or ($node_number >= 242)){
  print "Penguin,";
}
else {
  print "IBM,";
}


#
# Hardware model number
#
print "Unknown model number,";


#
# Serial number
#
print "Unknown serial number,";


#
# Ethernet IP
#
for my $line (`ip addr list dev eth0`){
  chomp $line;
  
  # Skip everything except the IP address
  next unless ($line =~ m/^\s+inet/);
  
  my $ip_address = (split(m/\s+/, $line))[2];
  
  # Remove the netmask
  $ip_address =~ s|/\d+||;
  
  print "$ip_address,";
}


#
# Rack location
#
print "Unknown rack\n";